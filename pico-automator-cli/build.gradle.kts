import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import java.io.BufferedWriter
import java.io.FileInputStream
import java.io.FileWriter
import java.util.Properties
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.shadowjar)
    id("jacoco")
}

val appGroupId = "com.github.aivanovski.cli"
val appVersion: String = libs.versions.appVersion.get()

group = appGroupId
version = appVersion

tasks.withType<KotlinCompile> {
    kotlinOptions {
        jvmTarget = "11"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.jacocoTestReport {
    reports {
        val coverageDir = File("$buildDir/reports/coverage")
        csv.required.set(true)
        csv.outputLocation.set(File(coverageDir, "coverage.csv"))
        html.required.set(true)
        html.outputLocation.set(coverageDir)
    }

    dependsOn(allprojects.map { it.tasks.named<Test>("test") })
}

tasks.test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

tasks {
    register<Copy>("copyClojureSources") {
        from("${project.rootDir.path}/pico-automator-clojure/src/picoautomator")
        into("${project.rootDir.path}/pico-automator-cli/src/main/resources/picoautomator")
    }

    register("createPropertyFileWithVersion") {
        doLast {
            val propertyName = "version"
            val propsFile = File("$projectDir/src/main/resources/version.properties")
            val props = Properties()

            if (propsFile.exists()) {
                props.load(FileInputStream(propsFile))
            }

            if (props[propertyName] != appVersion) {
                project.logger.lifecycle("Updating file: version.properties")
                props[propertyName] = appVersion
                props.store(BufferedWriter(FileWriter(propsFile)), "File is generated by Gradle")
            }
        }
    }

    named<ShadowJar>("shadowJar") {
        archiveBaseName.set("pico-automator-cli")
        mergeServiceFiles()
        manifest {
            attributes(mapOf("Main-Class" to "com.github.aivanovski.picoautomator.cli.MainKt"))
        }
    }
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    processResources {
        dependsOn("copyClojureSources")
        dependsOn("createPropertyFileWithVersion")
    }
}

dependencies {
    testImplementation(libs.junit.engine)
    testImplementation(libs.kotest.runner)
    testImplementation(libs.kotest.assertions)
    testImplementation(libs.mockk)

    implementation(project(":pico-automator"))
    implementation(libs.koin)
    implementation(libs.clojure)
}